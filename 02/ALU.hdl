// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
        // Zero x
        And16(a=x, b[0..15]=false, out=xZero);
        Mux16(a=x, b=xZero, sel=zx, out=xZeroDone);

        // Negate x 
        Not16(in=xZeroDone, out=xNeg);
        Mux16(a=xZeroDone, b=xNeg, sel=nx, out=xNegDone);

        // Zero y
        And16(a=y, b[0..15]=false, out=yZero);
        Mux16(a=y, b=yZero, sel=zy, out=yZeroDone);

        // Negate y 
        Not16(in=yZeroDone, out=yNeg);
        Mux16(a=yZeroDone, b=yNeg, sel=ny, out=yNegDone);


        // Adding or And-ing depending on f
        And16(a=xNegDone, b=yNegDone, out=outAnd);
        Add16(a=xNegDone, b=yNegDone, out=outAdd);
        Mux16(a=outAnd, b=outAdd, sel=f, out=outPost);

        // Negating out
        Not16(in=outPost, out=outNeg);
        Mux16(a=outPost, b=outNeg, sel=no, out=out, out[15]=ng, out[0..7]=outLow, out[8..15]=outHigh, out=outStatus);

        // Status flags
        Or8Way(in=outLow, out=lowNotZero);
        Or8Way(in=outHigh, out=highNotZero);
        Or(a=lowNotZero, b=highNotZero, out=outNotZero);
        Not(in=outNotZero, out=zr);
}